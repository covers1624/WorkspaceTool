import org.jetbrains.gradle.ext.ActionDelegationConfig.TestRunner

buildscript {
    repositories() {
        mavenLocal()
        jcenter()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.guardsquare:proguard-gradle:7.1.0'
        classpath 'gradle.plugin.org.cadixdev.gradle:licenser:0.6.1'
    }
}

plugins {
    id "org.jetbrains.gradle.plugin.idea-ext" version "1.0.1"
}

group 'net.covers1624.workspace_tool'
version '0.4.0'

version = "$version." + (System.getenv("BUILD_NUMBER") ?: "1")
println "Starting build of WorkspaceTool, Version: ${version}"

subprojects { subProject ->
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'org.cadixdev.licenser'

    group rootProject.group
    version rootProject.version

    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
        maven { url 'https://maven.covers1624.net/' }
        maven { url 'https://files.minecraftforge.net/maven/' }
        maven { url 'https://repo.gradle.org/gradle/libs-releases-local/' }
    }

    publishing {
        repositories {
            if (System.getenv('MAVEN_PASS')) {
                maven {
                    url "https://nexus.covers1624.net/repository/maven-releases/"
                    credentials {
                        username 'covers1624'
                        password System.getenv('MAVEN_PASS')
                    }
                }
            }
        }

        license {
            header = rootProject.file("LICENSE-header.txt")
            properties {
                year = new Date().format("yyyy")
            }
            newLine false

            include 'net/covers1624/wt/**'
            exclude 'net/covers1624/wt/util/pattern/**'
        }

        publications {
            publication(MavenPublication) {
                pom {
                    name = 'WorkspaceTool'
                    description = 'WorkspaceTool, A Minecraft Modding workspace tool.'
                    url = 'https://github.com/covers1624/WorkspaceTool'

                    scm {
                        url = 'https://github.com/covers1624/WorkspaceTool'
                        connection = 'scm:git:git://github.com/covers1624/WorkspaceTool.git'
                        connection = 'scm:git:git@github.com:covers1624/WorkspaceTool.git'
                    }

                    issueManagement {
                        system = 'github'
                        url = 'https://github.com/covers1624/WorkspaceTool/issues'
                    }
                    licenses {
                        license {
                            name = "MIT"
                            url = 'https://raw.githubusercontent.com/covers1624/WorkspaceTool/master/LICENSE.txt'
                            distribution = 'repo'
                        }
                    }
                    developers {
                        developer {
                            id = 'covers1624'
                            name = 'covers1624'
                        }
                    }
                }
            }
        }
    }
}

idea.project.settings {
    delegateActions {
        delegateBuildRunToGradle = false
        testRunner = TestRunner.PLATFORM
    }
}
