import org.gradle.api.attributes.java.TargetJvmEnvironment

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
        vendor = JvmVendorSpec.ADOPTIUM
    }
}

archivesBaseName = 'api'

dependencies {
    api loggerLibs.log4j2.api

    api project(":gradle-api")

    api libs.quack
    api(libs.guava) {
        attributes {
            attribute(
                    TargetJvmEnvironment.TARGET_JVM_ENVIRONMENT_ATTRIBUTE,
                    objects.named(TargetJvmEnvironment, TargetJvmEnvironment.STANDARD_JVM)
            )
        }
    }
    api(libs.jdkutils) {
        attributes {
            attribute(
                    Bundling.BUNDLING_ATTRIBUTE,
                    objects.named(Bundling, Bundling.EXTERNAL)
            )
        }
    }
    api libs.gson // Explicitly upgrade Gson transitive from jdkutils.
    api libs.jgit
}

publishing {
    publications {
        publication(MavenPublication) {
            artifactId archivesBaseName

            from components.java
        }
    }
}
